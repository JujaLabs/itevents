group 'org.itevents'
version '1.0-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

jar {
    archiveName = 'crawler.jar'
    destinationDir = file('../webapps')
}

repositories {
    mavenCentral()
}

findbugs {
    ignoreFailures = false
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = false
    ruleSetFiles = files("config/pmd/ruleset.xml")
}
tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    ignoreFailures = false
}
task checkstyleHtml << {
    ant.xslt(in: checkstyleMain.reports.xml.destination,
      style: file('config/checkstyle/checkstyle-noframes-sorted.xsl'),
      out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
}
checkstyleMain.finalizedBy checkstyleHtml


configurations {
    compile.extendsFrom configurations.springFramework
    compile.extendsFrom configurations.javaxInject
    compile.extendsFrom configurations.dao
    compile.extendsFrom configurations.log4j
    testCompile.extendsFrom configurations.testing
}

def testCrawlerLocalPropertiesFile = new File(projectDir.toString().replace('\\', '/') + "/src/test/resources/test-crawler-local.properties")
if (!testCrawlerLocalPropertiesFile.exists()) {
    throw new FileNotFoundException(testCrawlerLocalPropertiesFile.getCanonicalPath() + " not found")
}
def crawlerLocalPropertiesFile = new File(projectDir.toString().replace('\\', '/') + "/src/main/resources/crawler-local.properties")
if (!crawlerLocalPropertiesFile.exists()) {
    throw new FileNotFoundException(crawlerLocalPropertiesFile.getCanonicalPath() + " not found")
}

dependencies {
    compile project(':restservice')
    compile 'com.github.tomakehurst:wiremock:1.57'
    compile 'org.jsoup:jsoup:1.8.3'
    testCompile project(':restservice')
}
test.dependsOn ":restservice:liquibaseTestUpdate"

mainClassName = 'org.itevents.service.CrawlerService'